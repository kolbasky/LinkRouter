; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; Non-commercial use only

#define MyAppName "LinkRouter"
#define MyAppVersion "2.5.5"
#define MyAppPublisher "kolbasky"
#define MyAppURL "https://github.com/kolbasky/LinkRouter"
#define MyAppExeName "linkrouter-gui.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{8FE57B93-D8AF-45BF-A6D9-B66432E6B658}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
CloseApplications=yes
CloseApplicationsFilter=linkrouter.exe,linkrouter-gui.exe
DefaultDirName={autopf}\{#MyAppName}
UninstallDisplayIcon={app}\{#MyAppExeName}
; "ArchitecturesAllowed=x64compatible" specifies that Setup cannot run
; on anything but x64 and Windows 11 on Arm.
ArchitecturesAllowed=x64compatible
; "ArchitecturesInstallIn64BitMode=x64compatible" requests that the
; install be done in "64-bit mode" on x64 or Windows 11 on Arm,
; meaning it should use the native 64-bit Program Files directory and
; the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64compatible
DisableProgramGroupPage=yes
; Remove the following line to run in administrative install mode (install for all users).
PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
OutputDir=bin\
OutputBaseFilename=LinkRouter-Installer
SetupIconFile=cmd\linkrouter\linkrouter.ico
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Code]
procedure CurPageChanged(CurPageID: Integer);
begin
  if CurPageID = wpReady then
  begin
    if WizardIsTaskSelected('runasadmin')  then
    begin
      if not IsAdmin then
      begin
        MsgBox('To enable "Always run as administrator", you must restart the installer with admin privs. Right-click it and select "Run as administrator".', mbError, MB_OK);
        WizardForm.NextButton.Enabled := False;
        WizardForm.BackButton.Enabled := True;
        WizardForm.BackButton.OnClick(nil);
      end;
    end;
  end;
end;

function RunAsAdminChecked: Boolean;
begin
  Result := WizardIsTaskSelected('runasadmin');
end;

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "copyextension"; Description: "Copy unpacked browser-extension for Chromium-based browsers to app folder. Use developer mode and load an upacked extesnion to install in browser."; GroupDescription: "Other options:"; Flags: checkedonce
Name: "runasadmin"; Description: "Always run LinkRouter as administrator (required only if some rules launch apps that need elevated privileges)"; GroupDescription: "Other options:"; Flags: unchecked

[Files]
Source: "bin\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "bin\linkrouter.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "bin\linkrouter-extension.zip"; DestDir: "{app}"; Flags: ignoreversion; Tasks: copyextension

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Registry]
Root: HKCU; Subkey: "Software\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers"; \
  ValueType: string; ValueName: "{app}\linkrouter.exe"; ValueData: "RUNASADMIN"; \
  Tasks: runasadmin; Check: IsWin64; Flags: uninsdeletevalue

Root: HKCU; Subkey: "Software\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers"; \
  ValueType: string; ValueName: "{app}\linkrouter-gui.exe"; ValueData: "RUNASADMIN"; \
  Tasks: runasadmin; Flags: uninsdeletevalue

[Run]
Filename: "{app}\linkrouter.exe"; Parameters: "--register --quiet"; Flags: runhidden runasoriginaluser; Check: not RunAsAdminChecked
Filename: "{app}\linkrouter.exe"; Parameters: "--register --quiet"; Flags: runhidden runascurrentuser; Check: RunAsAdminChecked
Filename: "powershell.exe"; Parameters: "-NoProfile -ExecutionPolicy Bypass -Command ""Expand-Archive -Path '{app}\linkrouter-extension.zip' -DestinationPath '{app}\chrome_extension' -Force; Remove-Item -Path '{app}\linkrouter-extension.zip'"""; Flags: runhidden; Tasks: copyextension
Filename: "{app}\{#MyAppExeName}"; Description: "Launch LinkRouter GUI"; Flags: nowait postinstall skipifsilent runasoriginaluser; Check: not RunAsAdminChecked
Filename: "{app}\{#MyAppExeName}"; Description: "Launch LinkRouter GUI"; Flags: nowait postinstall skipifsilent runascurrentuser; Check: RunAsAdminChecked
Filename: "{win}\explorer.exe"; Parameters: """ms-settings:defaultapps?registeredAppUser=LinkRouter"""; Description: "Show ""Default Apps"" dialog"; Flags: nowait postinstall

[UninstallRun]
Filename: "{app}\linkrouter.exe"; Parameters: "--unregister"; Flags: runhidden; RunOnceId: "unregisterLinkRouter"
Filename: "{cmd}"; Parameters: "/C taskkill /IM linkrouter-gui.exe /F /T"; Flags: runhidden; RunOnceId: "killLinkRouterGUI"
Filename: "{cmd}"; Parameters: "/C taskkill /IM linkrouter.exe /F /T"; Flags: runhidden; RunOnceId: "killLinkRouter"

[UninstallDelete]
Type: filesandordirs; Name: "{app}\chrome_extension"
